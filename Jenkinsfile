pipeline {
    agent any

    environment {
        SERVICE_NAME    = 'frontend'
        PROJECT_ID      = 'velvety-calling-458402-c1'
        REGION          = 'asia-northeast3'
        GAR_HOST        = 'asia-northeast3-docker.pkg.dev'
        CONTAINER_NAME  = 'frontend'
        PORT            = '3000'
        SSH_KEY_PATH    = '/var/jenkins_home/.ssh/id_rsa'
        SSH_USER        = 'peter'
    }

    stages {
        stage('Set Branch & Cron Trigger') {
            steps {
                script {
                    def branchName = env.BRANCH_NAME ?: env.GIT_BRANCH?.replaceFirst(/^origin\//, '')
                    env.BRANCH = branchName

                    if (branchName == 'main') {
                        properties([pipelineTriggers([cron('30 0 * * 1-5')])])
                    } else if (branchName == 'dev') {
                        properties([pipelineTriggers([
                            cron('30 3 * * 1-4'),
                            cron('0 1 * * 5'),
                            cron('30 3 * * 6,7')
                        ])])
                    } else {
                        properties([pipelineTriggers([])])
                        echo "‚õî ÏßÄÏõêÎêòÏßÄ ÏïäÎäî Î∏åÎûúÏπòÏûÖÎãàÎã§: ${branchName}. ÎπåÎìúÎ•º Ï§ëÎã®Ìï©ÎãàÎã§."
                        currentBuild.result = 'NOT_BUILT'
                        error("Unsupported branch: ${branchName}")
                    }
                }
            }
        }

        stage('Notify Before Start') {
            when {
                expression { env.BRANCH in ['main', 'dev'] }
            }
            steps {
                script {
                    withCredentials([string(credentialsId: 'Discord-Webhook', variable: 'DISCORD')]) {
                        discordSend(
                            description: "üöÄ Î∞∞Ìè¨Í∞Ä Í≥ß ÏãúÏûëÎê©ÎãàÎã§: ${env.SERVICE_NAME} - ${env.BRANCH} Î∏åÎûúÏπò",
                            link: env.BUILD_URL,
                            title: "Î∞∞Ìè¨ ÏãúÏûë",
                            webhookURL: "$DISCORD"
                        )
                    }
                }
            }
        }

        stage('Checkout') {
            steps {
                checkout scm
            }
        }

        stage('Set Environment by Branch') {
            steps {
                script {
                    if (env.BRANCH == 'main') {
                        env.FE_PRIVATE_IP = '10.10.20.2'
                        env.ENV_LABEL = 'prod'
                        env.REPO_NAME = 'dolpin-docker-image-prod'
                        env.API_BASE_CRED_ID = 'NEXT_PUBLIC_API_BASE_PROD'
                    } else {
                        env.FE_PRIVATE_IP = '10.20.20.2'
                        env.ENV_LABEL = 'dev'
                        env.REPO_NAME = 'dolpin-docker-image-dev'
                        env.API_BASE_CRED_ID = 'NEXT_PUBLIC_API_BASE_DEV'
                    } 

                    env.TAG = "${env.SERVICE_NAME}:${env.BUILD_NUMBER}"
                    env.GAR_IMAGE = "${env.GAR_HOST}/${env.PROJECT_ID}/${env.REPO_NAME}/${env.TAG}"
                }
            }
        }

        stage('Load Secrets') {
            steps {
                script {
                    // Jenkins Credential PluginÏùÑ ÌÜµÌï¥ ÌôòÍ≤ΩÎ≥ÄÏàò Î°úÎìú
                    withCredentials([
                        string(credentialsId: "${env.API_BASE_CRED_ID}", variable: 'API_BASE_URL'),
                        string(credentialsId: 'NEXT_PUBLIC_KAKAOMAP_KEY', variable: 'KAKAOMAP_KEY')
                    ]) {
                        env.API_BASE_URL = API_BASE_URL
                        env.KAKAOMAP_KEY = KAKAOMAP_KEY
                    }
                }
            }
        }


        stage('GAR Ïù∏Ï¶ù') {
            steps {
                sh "gcloud auth configure-docker ${env.GAR_HOST} --quiet"
            }
        }

        stage('Docker Build & Push to GAR') {
            steps {
                sh """
                    docker build \
                      --build-arg NEXT_PUBLIC_API_BASE_URL=${env.API_BASE_URL} \
                      --build-arg NEXT_PUBLIC_KAKAOMAP_KEY=${env.KAKAOMAP_KEY} \
                      -t ${env.GAR_IMAGE} .
                    docker push ${env.GAR_IMAGE}
                """
            }
        }

        stage('Deploy to FE via SSH') {
            steps {
                script {
                    def saCredId = env.BRANCH == 'main' ? 'fe-sa-key-prod' : 'fe-sa-key-dev'

                    // GCP Secret ManagerÏóêÏÑú ÏÑúÎπÑÏä§ Í≥ÑÏ†ï ÌÇ§ Îã§Ïö¥Î°úÎìú
                    sh """
                    gcloud secrets versions access latest \
                    --secret="${saCredId}" \
                    --project="${env.PROJECT_ID}" > gcp-key.json
                    """

                    def deployScript = """
#!/bin/bash
set -e

export HOME=/home/${env.SSH_USER}

mv /tmp/gcp-key.json \$HOME/gcp-key.json
chown ${env.SSH_USER}:${env.SSH_USER} \$HOME/gcp-key.json
chmod 600 \$HOME/gcp-key.json

# ÏÑúÎπÑÏä§ Í≥ÑÏ†ï Ïù∏Ï¶ù Î∞è docker Ïù∏Ï¶ù
gcloud auth activate-service-account --key-file="\$HOME/gcp-key.json"
gcloud config set project ${env.PROJECT_ID} --quiet
gcloud auth configure-docker ${env.GAR_HOST} --quiet
gcloud auth print-access-token | docker login -u oauth2accesstoken --password-stdin https://${env.GAR_HOST}

sudo docker stop ${env.CONTAINER_NAME} || true
sudo docker rm ${env.CONTAINER_NAME} || true

docker pull ${env.GAR_IMAGE}

sudo docker run -d --name ${env.CONTAINER_NAME} \\
  -p ${env.PORT}:${env.PORT} \\
  ${env.GAR_IMAGE}
"""
                    // Jenkins ÏõåÌÅ¨Ïä§ÌéòÏù¥Ïä§Ïóê Î∞∞Ìè¨ Ïä§ÌÅ¨Î¶ΩÌä∏ ÌååÏùº Ï†ÄÏû•
                    writeFile file: 'deploy.sh', text: deployScript
                    sh "chmod 600 ${env.SSH_KEY_PATH}"
                    
                    // ÌÇ§ÏôÄ Ïä§ÌÅ¨Î¶ΩÌä∏ Ï†ÑÏÜ° ÌõÑ Ïã§Ìñâ
                    sh """
scp -i ${env.SSH_KEY_PATH} -o StrictHostKeyChecking=no gcp-key.json ${env.SSH_USER}@${env.FE_PRIVATE_IP}:/tmp/gcp-key.json
scp -i ${env.SSH_KEY_PATH} -o StrictHostKeyChecking=no deploy.sh ${env.SSH_USER}@${env.FE_PRIVATE_IP}:/tmp/deploy.sh

ssh -tt -i ${env.SSH_KEY_PATH} -o StrictHostKeyChecking=no ${env.SSH_USER}@${env.FE_PRIVATE_IP} "bash /tmp/deploy.sh"
"""
                }
            }
        }
    }

    post {
        success {
            script {
                if (env.BRANCH in ['main', 'dev']) {
                    withCredentials([string(credentialsId: 'Discord-Webhook', variable: 'DISCORD')]) {
                        discordSend description: """
                        Ï†úÎ™© : ${currentBuild.displayName}
                        Í≤∞Í≥º : ${currentBuild.result}
                        Ïã§Ìñâ ÏãúÍ∞Ñ : ${currentBuild.duration / 1000}s
                        """,
                        link: env.BUILD_URL, result: currentBuild.currentResult,
                        title: "${env.JOB_NAME} : ${currentBuild.displayName} ÏÑ±Í≥µ",
                        webhookURL: "$DISCORD"
                    }
                }
            }
        }
        failure {
            script {
                if (env.BRANCH in ['main', 'dev']) {
                    withCredentials([string(credentialsId: 'Discord-Webhook', variable: 'DISCORD')]) {
                        discordSend description: """
                        Ï†úÎ™© : ${currentBuild.displayName}
                        Í≤∞Í≥º : ${currentBuild.result}
                        Ïã§Ìñâ ÏãúÍ∞Ñ : ${currentBuild.duration / 1000}s
                        """,
                        link: env.BUILD_URL, result: currentBuild.currentResult,
                        title: "${env.JOB_NAME} : ${currentBuild.displayName} Ïã§Ìå®",
                        webhookURL: "$DISCORD"
                    }
                }
            }
        }
    }
}
